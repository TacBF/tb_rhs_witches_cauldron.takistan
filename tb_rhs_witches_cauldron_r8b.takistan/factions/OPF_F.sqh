// JAF - Army (Arid)
// Author: Cole

#include "\ice\ice_gear\kitDefines.sqh"

private ["_result", "_role", "_side", "_faction",
	"_weapons", "_attachments", "_magazines", "_commonOrdnance",
	"_uniform", "_vest", "_backpack",
	"_uniformItems", "_vestItems", "_backpackItems",
	"_assignedItems", "_helmet", "_glasses", "_nvGoggles", "_binoculars", "_nvGogglesForAll"];

_role = _this select 0;

//====================================================//
//				  Default Loadout                     // 
//====================================================//

	// Up to 3 weapons = ["primary", "secondary", "handgun"]
	_weapons = ["rhs_weap_m16a4_carryhandle"];
	// Up to 3 attachments per weapon = [["primary"], ["secondary"], ["handgun"]]
	_attachments = [["rhsusf_acc_eotech_552", "rhsusf_acc_SF3P556"], [], []];
	
	// Magazines for all weapons, unlimited array, added to any container with space available.
	_magazines = [["rhs_mag_30Rnd_556x45_M855A1_Stanag", 8]];
	// Common gear items, these will be added to any container with space available.
	_commonOrdnance = [["rhs_mag_m67", 2],["SmokeShell", 2],["ICE_FieldDressing", 4],["ICE_Adrenaline", 2],["ICE_Morphine", 1]];
	
	// Containers
	_uniform = "O_CombatUniform_TB_JOR_Arid";
	_vest = "rhs_6b13_6sh92";
	_backpack = "";

	_helmet = "H_HelmetIA_JOR_Arid";
	_glasses = "";
	_assignedItems = ["ItemMap", "ItemRadio", "ItemCompass", "ItemWatch"];
	_nvGoggles = if (__isNight) then {"NVGoggles";} else {"";};
	_binoculars = "Binocular";
	
	// optional alternate method, which allows you to list items specifically for this container type. Otherwise use "_magazines" and/or "_commonOrdnance" instead. If using both, ensure you do not double up items.
	_uniformItems = [];
	_vestItems = [];
	_backpackItems = [];

//====================================================//
//					Kit Overrides                     // 
//====================================================//

switch (_role) do
{
	case __ICE_gear_Rifleman:
	{
		_weapons = ["rhs_weap_ak74m_desert_npz"];
		_attachments = [["optic_MRCO", "rhs_acc_dtk"], [], []];
		_magazines = [["rhs_30Rnd_545x39_AK", 8]];
	};
	
	case __ICE_gear_Grenadier:
	{
		_weapons = ["rhs_weap_m16a4_carryhandle_M203"];
		_vest = "rhs_6b13_6sh92_vog";
		_magazines = if (__isNight) then {[["rhs_mag_30Rnd_556x45_M855A1_Stanag", 8], ["rhs_mag_M441_HE", 8], ["rhs_mag_m661_green", 4]];} else {[["rhs_mag_30Rnd_556x45_M855A1_Stanag", 8], ["rhs_mag_M441_HE", 8], ["rhs_mag_m714_White", 4]];};
	};
	
	case __ICE_gear_MG:
	{
		_weapons = ["rhs_weap_m240B_CAP"];
		_attachments = [[], [], []];
		_backpack = "B_Kitbag_cbr";
		_magazines = [["rhsusf_100Rnd_762x51", 3]];
	};
	
	case __ICE_gear_Medic:
	{
		_commonOrdnance = [["rhs_mag_m67", 2], ["SmokeShell", 8], ["ICE_FieldDressing", 20], ["ICE_Adrenaline", 20], ["ICE_Morphine", 10]];
	};
	
	case __ICE_gear_CombatEngineer:
	{
		_backpack = "B_Kitbag_cbr";
		_backpackItems = [["DemoCharge_Remote_Mag", 4]];
	};
	
	case __ICE_gear_LightAT:
	{
		_weapons pushback "rhs_weap_rpg26";
		_backpack = "B_Kitbag_cbr";
	};
	
	case __ICE_gear_HeavyAT:
	{
		_weapons pushback "rhs_weap_rpg7";
		_attachments set [1, ["rhs_acc_pgo7v"]];
		_magazines pushback ["rhs_rpg7_PG7VL_mag", 3];
		_backpack = "B_Carryall_cbr";
	};
	
	case __ICE_gear_AA:
	{
		_weapons pushback "rhs_weap_igla";
		_magazines pushback ["rhs_mag_9k38_rocket", 1];
		_backpack = "B_Carryall_cbr";
	};
	
	case __ICE_gear_Marksman:
	{
		_weapons = ["rhs_weap_m14ebrri"];
		_attachments = [["optic_SOS", "rhsusf_acc_harris_bipod"], [], []];
		_magazines = [["rhsusf_20Rnd_762x51_m118_special_Mag", 8]];
		_binoculars = "lerca_1200_tan";
	};
	
	case __ICE_gear_Sniper:
	{
		_weapons = ["srifle_GM6_camo_F", "rhsusf_weap_glock17g4"];
		_attachments = [["rhs_acc_dh520x56"], [], []];
		_magazines = [["5Rnd_127x108_Mag", 5], ["rhsusf_mag_17Rnd_9x19_JHP", 3]];
		_assignedItems pushback "ItemGPS";
		_commonOrdnance append [["SmokeShellRed", 2], ["LaserBatteries",2]];
		_binoculars = "TB_laserDesignatorNVG";
		_helmet = "H_Booniehat_pan";
		_backpack = "tf_mr3000_bwmod_tropen";
	};
	
	case __ICE_gear_SL:
	{
		_weapons = ["rhs_weap_ak74m_desert_npz"];
		_attachments = [["optic_Hamr", "rhs_acc_dtk"], [], []];
		_magazines = [["rhs_30Rnd_545x39_AK", 8]];
		_backpack = "tf_mr3000_bwmod_tropen";
		_assignedItems pushback "ItemGPS";
		_commonOrdnance append [["SmokeShellRed", 2], ["LaserBatteries",2]];
		_binoculars = "TB_laserDesignatorNVG";
		_vest = "rhs_6b13_6sh92_radio";
	};
	
	case __ICE_gear_Crewman:
	{
		_attachments = [[], [], []];
		_magazines = [["rhs_mag_30Rnd_556x45_M855A1_Stanag", 5]];
		_assignedItems pushback "ItemGPS";
		_backpack = "tf_mr3000_bwmod_tropen";
		_helmet = "rhsusf_cvc_green_helmet";
		_vest = "rhs_6b13_crewofficer";
		_commonOrdnance pushback ["SmokeShellRed", 2];
		_binoculars = "lerca_1200_tan";
	};
	
	case __ICE_gear_Pilot:
	{
		_attachments = [[], [], []];
		_magazines = [["rhs_mag_30Rnd_556x45_M855A1_Stanag", 5]];
		_assignedItems pushback "ItemGPS";
		_uniform = "U_I_HeliPilotCoveralls";
		_helmet = "rhs_zsh7a_mike";
		_vest = "rhs_vydra_3m";
		_commonOrdnance pushback ["SmokeShellRed", 2];
		_backpack = "B_Parachute";
	};
	
	case __ICE_gear_Diver:
	{
		_backpack = "O_UAV_01_backpack_F";
		_assignedItems pushback "O_UavTerminal";
	};
};

//-----------------------------------------------------------------------------
_nvGogglesForAll = [["gear", "NVGogglesForAll"], "east", 1, true] call ICE_fnc_getSetting;
if (_nvGogglesForAll == 0) then {_nvGoggles = "";};
if (_nvGoggles != "") then { _assignedItems pushBack _nvGoggles };
if (_binoculars != "") then { _assignedItems pushBack _binoculars };
//-----------------------------------------------------------------------------
_result =
[
	_weapons,
	_attachments, // nested array
	_magazines, // nested array - in A3, all magazines should be in each container's items list.
	_commonOrdnance, // combined nested/single array - in A3, all magazines should be in each container's items list.

	_uniform,
	_vest,
	_backpack,

	_uniformItems,
	_vestItems,
	_backpackItems,

	_assignedItems,
	_helmet,
	_glasses
];
//-----------------------------------------------------------------------------
_result